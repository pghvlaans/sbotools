#!/usr/bin/perl
#
# vim: ts=4:noet
#
# sbohints
# script to modify the blacklist and optional dependencies
#
# authors: Jacob Pipkin <j@dawnrazor.net>
#          Luke Williams <xocel@iquidus.org>
#          Andreas Guldstrand <andreas.guldstrand@gmail.com>
# maintainer: K. Eugene Carlson <kvngncrlsn@gmail.com>
# license: MIT License

use 5.16.0;
use strict;
use warnings FATAL => 'all';
use SBO::Lib qw/ auto_reverse get_optional lint_sbo_config on_blacklist open_fh open_read prompt read_hints script_error show_version uniq usage_error wrapsay %config $conf_dir @listings /;
use File::Basename;
use Getopt::Long qw(:config no_ignore_case_always bundling);

my $self = basename($0);

sub show_usage {
	print <<"EOF";
Usage: $self [options] [arguments]

Options:
  -h|--help:
    this screen.
  -v|--version:
    version information.
  -l|--list:
    show current blacklist and optional dependencies.
  -q|--query:
    show the hint status of one or more scripts.
  -b|--blacklist:
    add scripts to the blacklist (clear with c).
  -o|--optional:
    add optional dependencies to scripts (clear with c).
  -O|--replace-optional:
    replace all optional dependencies for scripts (clear with c).
  -r|--reverse:
    add reverse dependency rebuild requests for scripts (clear with c).
  --reset:
    empty the blacklist and optional dependencies.

  -c|--clear:
    clear instead of add.

  Examples:
  $self -b libsystemd sbotools
  $self -cb vifm
  $self -q qemu

EOF
}

my $hintfile = "$conf_dir/sbotools.hints";

my ($help, $version, $list, $reset, $reverse, $optional, $replace_optional, $blacklist, $clear, $query);

GetOptions(
	'help|h'              => \$help,
	'version|v'           => \$version,
	'list|l'              => \$list,
	'reset'               => \$reset,
	'reverse|r'           => \$reverse,
	'optional|o'          => \$optional,
	'replace_optional|O'  => \$replace_optional,
	'clear|c'             => \$clear,
	'query|q'             => \$query,
	'blacklist|b'         => \$blacklist,
);

if ($help) {
	show_usage();
	wrapsay "Non-root users can only call $self with -l, -q, -h and -v." unless $< == 0;
	exit 0;
}
if ($version) { show_version(); exit 0; }
unless ($< == 0 or $list or $query) {
	show_usage();
	usage_error "Non-root users can only call $self with -l, -q, -h and -v.";
}
unless (@ARGV or $list or $reset) { show_usage(); exit 0; }

lint_sbo_config($self, %config);

if (($blacklist and ($optional or $replace_optional or $reverse)) or ($optional and $replace_optional) or ($optional and $reverse) or ($replace_optional and $reverse)) {
	usage_error("optional, replace_optional, reverse and blacklist cannot be used together.");
}

if ($list) { show_list(); exit 0; }

for my $item (@ARGV) {
	usage_error "Separate compat32 requests are not needed." if $item =~ /-compat32$/;
}

if ($query) { make_query(); exit 0; }

if ($reset and ($optional or $clear or $replace_optional or $blacklist or $clear or $reverse)) {
	usage_error("reset cannot be used with other flags.");
}

if ($reset) {
	if (prompt("Are you sure you want to clear all scripts from the blacklist and all optional dependency requests?", default => 'no')) {
		wrapsay "Clearing the blacklist and all optional depedency requests...";
		reset_all();
	} else {
		exit 0;
	}
}

if ($replace_optional or $optional) {
	unless (@ARGV) { show_usage(); exit 0; }
	my $made_output;
	for my $target (uniq @ARGV){
		print "\n" if $made_output;
		$made_output = 1;
		prompt_queue($target);
	}
}

if ($blacklist or $reverse) {
	unless (@ARGV) { show_usage(); exit 0; }
	for my $target (uniq @ARGV) { alter_designation($target); }
}

# Takes one script to be added or cleared at a time. If flag
# -b or -cb, alter the blacklist. If flag -r or -cr, add or clear
# reverse dependency rebuild requests.
sub alter_designation {
	script_error("alter_designation requires an argument.") unless @_;
	my $sbo = shift;
	if ($clear and $blacklist and not on_blacklist($sbo)) {
		wrapsay "$sbo is not on the blacklist.";
		return;
	}
	if ($blacklist and on_blacklist($sbo) and not $clear) {
		wrapsay "$sbo is already on the blacklist.";
		return;
	}
	if ($clear and $reverse and not auto_reverse($sbo)) {
		wrapsay "$sbo does not have a reverse dependency rebuild request.";
		return;
	}
	if ($reverse and auto_reverse($sbo) and not $clear) {
		wrapsay "$sbo already has a reverse dependency rebuild request.";
		return;
	}
	if ($clear) {
		my @incoming;
		# it is not possible to reach this point unless
		# /etc/sbotools/sbotools.hints exists
		my ($rfh, $rexit) = open_read($hintfile);
		if ($rexit) {
			warn $rfh;
			exit $rexit;
		}
		# flag -cb/-cr; only write back lines that don't alter the
		# designation for $sbo
		if ($blacklist) {
			wrapsay "Clearing $sbo from the blacklist...";
			for my $line (<$rfh>) { push @incoming, $line unless $line =~ m/^\!$sbo$/; }
		} else {
			wrapsay "Clearing reverse dependency rebuild request for $sbo...";
			for my $line (<$rfh>) { push @incoming, $line unless $line =~ m/^\~$sbo$/; }
		}
		close $rfh;
		my ($fh, $exit) = open_fh($hintfile, ">");
		if ($exit) {
			warn $fh;
			exit $exit;
		}
		print {$fh} @incoming;
		close $fh;
	} else {
		mkdir $conf_dir unless -d $conf_dir;
		system(qw/ touch /, $hintfile) unless -f $hintfile;
		my ($fh, $exit) = open_fh($hintfile, ">>");
		if ($exit) {
			warn $fh;
			exit $exit;
		}
		# flag -b/-r; print a new line blacklisting/requesting reverse
		# rebuilds for $sbo
		if ($blacklist) {
			wrapsay "Blacklisting $sbo...";
			print {$fh} "!$sbo\n";
		} else {
			wrapsay "Requesting reverse dependency rebuilds for $sbo...";
			print {$fh} "~$sbo\n";
		}
		close $fh;
	}
}

sub make_query {
	unless (@ARGV) { show_usage(); exit 0; }
	my $made_output;
	for my $sbo (uniq @ARGV) {
		print "\n" if $made_output;
		if (on_blacklist($sbo)) {
			say "$sbo is blacklisted.";
			$made_output = 1;
		}
		if (auto_reverse($sbo)) {
			wrapsay "$sbo has automatic reverse dependency rebuilding.";
			$made_output = 1;
		}
		if (my @optionals = get_optional($sbo)) {
			if (@optionals == 1) {
				wrapsay "Optional dependency request for $sbo:";
			} else {
				wrapsay "Optional dependency requests for $sbo:";
			}
			wrapsay(join(" ", @optionals));
			$made_output = 1;
		} else {
			my @loclistings = @listings;
			my @optional_for;
			if (grep { /(^|\s)$sbo\s/ } @loclistings) {
				FIRST: for my $hinted (@loclistings) {
					next FIRST if $hinted =~ m/^(!|\s|#)/;
					next FIRST if $hinted =~ m/\s$/;
					next FIRST unless $hinted =~ m/(^|\s)$sbo\s/;
					my @items = split(' ', $hinted);
					my $result = pop @items;
					push @optional_for, $result unless grep { /^$result$/ } @optional_for;
				}
				if (@optional_for) {
					$made_output = 1;
					if (@optional_for == 1) {
						my $requested = shift @optional_for;
						wrapsay "$sbo is an optional dependency of $requested.";
					} else {
						wrapsay "The following scripts optionally depend on $sbo:";
						@optional_for = sort @optional_for;
						for my $result (@optional_for) { wrapsay "$result"; }
					}
				}
			}
		}
	}
}

# Get user input for optional dependency modifications.
sub prompt_queue {
	script_error("prompt_queue requires an argument.") unless @_;
	my $sbo = shift;
	my (@incoming, $message);
	$message = "Select one or more scripts to add, or leave blank to skip: ";
	$message = "Select one or more scripts to clear, or leave blank to skip: " if $clear;
	$message = "Enter the new optional dependency list, or leave blank to skip: " if $replace_optional;
	$message = "Proceed with clearing the optional dependency list for $sbo?" if $replace_optional and $clear;

	my @current = get_optional($sbo);
	if (@current) {
		wrapsay "Current optional dependencies for $sbo:";
		wrapsay(join(" ", @current), 1);
		# flags -co, -o, -O
		unless ($replace_optional and $clear) {
			if (@incoming = split(" ", prompt($message))) {
				if (grep { /-compat32$/ } @incoming) {
					wrapsay("Separate -compat32 requests are not needed.", 1);
					prompt_queue($sbo);
					return;
				}
				if (grep { /\!/ } @incoming) {
					wrapsay("! cannot be used in optional dependency requests.", 1);
					prompt_queue($sbo);
					return;
				}
				if (grep { /^$sbo$/ } @incoming) {
					if ($clear) {
						wrapsay("Cannot clear $sbo from its own list.", 1);
					} else {
						wrapsay("$sbo cannot be its own optional dependency.", 1);
					}
					prompt_queue($sbo);
					return;
				}
				my $is_needed;
				# for -o and -co, don't attempt changes if unneeded
				if ($clear) {
					for my $item (@incoming) { $is_needed = 1 if grep { /^$item$/ } @current; }
				} elsif ($optional) {
					for my $item (@incoming) { $is_needed = 1 unless grep { /^$item$/ } @current; }
				} else { $is_needed = 1; }
				if ($is_needed) {
					write_optional($sbo, @incoming) if $is_needed;
				} else {
					wrapsay "No changes to be made to $sbo; skipping.";
				}
				return;
			} else {
				say "Skipping.";
				return;
			}
		} elsif (prompt($message, default => 'no')) {
			# flag -cO
			write_optional($sbo);
			return;
		} else {
			say "Skipping.";
			return;
		}
	} elsif ($clear) {
		# flags -co and -cO, no existing entry
		wrapsay "No optional dependency requests to clear for $sbo.";
		return;
	} else {
		# flags -o and -O, no existing entry
		wrapsay("$sbo has no optional dependency requests.", 1);
		if (@incoming = split(" ", prompt($message))) {
			if (grep { /-compat32$/ } @incoming) {
				wrapsay("Separate -compat32 requests are not needed.", 1);
				prompt_queue($sbo);
				return;
			}
			if (grep { /\!/ } @incoming) {
				wrapsay("! cannot be used in optional dependency requests.", 1);
				prompt_queue($sbo);
				return;
			}
			if (grep { /^$sbo$/ } @incoming) {
				if ($clear) {
					wrapsay("Cannot clear $sbo from its own list.", 1);
				} else {
					wrapsay("$sbo cannot be its own optional dependency.", 1);
				}
				prompt_queue($sbo);
				return;
			}
			write_optional($sbo, @incoming);
			return;
		} else {
			say "Skipping.";
			return;
		}
	}
}

# Remove any line from sbotools.hints that isn't commented or whitespace-
# initial.
sub reset_all {
	my @loclistings = @listings;
	my @remaining;
	unless (@loclistings) {
		wrapsay "No blacklist or optional dependency requests found.";
		exit 0;
	}
	my ($rfh, $rexit) = open_read($hintfile);
	if ($rexit) {
		warn $rfh;
		exit $rexit;
	}
	for my $line (<$rfh>) {
		chomp(my $search = $line);
		push @remaining, $line unless grep { /^$search$/ } @loclistings;
	}
	close $rfh;
	my ($fh, $exit) = open_fh($hintfile, '>');
	if ($exit) {
		warn $fh;
		exit $exit;
	}
	print {$fh} @remaining;
	close $fh;
}

# Write out the blacklist, followed by optional dependency requests
# per script. If anything on the blacklist has requests, or if a script
# in a list of optional dependencies is blacklisted, note that fact.
sub show_list {
	my @loclistings = @listings;
	my $message = "The blacklist is empty and no optional dependency or reverse dependency rebuild requests could be identified.";
	unless (@loclistings) {
		wrapsay $message;
		exit 0;
	}
	my (@candlist, @ineffectuals, @ineffectual_requests);
	for my $listing (@loclistings) {
		$listing =~ s/^(\!|~)//;
		my @members = split(" ", $listing);
		next if not @members;
		for my $cand (@members) { push @candlist, $cand; }
	}
	unless (@candlist) {
		wrapsay $message;
		exit 0;
	}
	@candlist = uniq @candlist;
	@candlist = sort @candlist;
	my ($wrote_blacklist, $wrote_auto_reverse, $wrote_optional, @blacklisted, @has_auto_reverse);
	for my $sbo (@candlist) {
		if (on_blacklist($sbo)) {
			push @blacklisted, $sbo unless grep {/^$sbo$/} @blacklisted;
			$wrote_blacklist = 1;
		}
		if (auto_reverse($sbo)) {
			push @has_auto_reverse, $sbo unless grep {/^$sbo$/} @has_auto_reverse;
			$wrote_auto_reverse = 1;
		}
	}
	if (@blacklisted == 1) {
		my $result = shift @blacklisted;
		wrapsay "$result is blacklisted.";
		$wrote_blacklist = 1;
	} elsif (@blacklisted) {
		wrapsay "The following scripts are blacklisted:";
		for my $sbo (@blacklisted) {
			say "$sbo";
		}
		$wrote_blacklist = 1;
	}
	if (@has_auto_reverse == 1) {
		say " " if $wrote_blacklist;
		my $result = shift @has_auto_reverse;
		wrapsay "$result has reverse dependency rebuilding.";
		$wrote_auto_reverse = 1;
	} elsif (@has_auto_reverse) {
		say " " if $wrote_blacklist;
		wrapsay "The following scripts have reverse dependency rebuilding:";
		for my $sbo (@has_auto_reverse) {
			say "$sbo";
		}
		$wrote_auto_reverse= 1;
	}
	for my $sbo (@candlist) {
		if (my @optionals = get_optional($sbo)) {
			say " " if $wrote_blacklist or $wrote_optional or $wrote_auto_reverse;
			if (@optionals == 1) {
				say "Optional dependency request for $sbo:";
			} else {
				say "Optional dependency requests for $sbo:";
			}
			wrapsay(join(" ", @optionals));
			push @ineffectuals, $sbo if on_blacklist($sbo);
			for my $request (@optionals) {
				push @ineffectual_requests, $request if on_blacklist($request);
			}
			$wrote_optional = 1;
		}
	}
	if (@ineffectuals) {
		@ineffectuals = sort @ineffectuals;
		say " ";
		wrapsay "The following scripts have optional dependency requests, but are blacklisted:";
		wrapsay(join("\n", @ineffectuals));
	}
	if (@ineffectual_requests) {
		@ineffectual_requests = uniq sort(@ineffectual_requests);
		say " ";
		wrapsay "The following scripts are requested as optional dependencies, but are blacklisted:";
		wrapsay(join("\n", @ineffectual_requests));
	}
	wrapsay $message unless $wrote_blacklist or $wrote_optional;
	exit 0;
}

# Takes the name of a SlackBuild and an array with scripts to add or
# clear (except for -cO).
sub write_optional {
	script_error("write_optional requires at least one argument.") unless @_;
	my ($sbo, @operation) = @_;
	script_error("write_optional needs two arguments except for clear operations.") unless @operation or $clear;
	mkdir $conf_dir unless -d $conf_dir;
	read_hints() if -f $hintfile;
	system(qw/ touch /, $hintfile) unless -f $hintfile;
	@operation = uniq @operation if @operation;
	my @loclistings = @listings;
	my ($rfh, $rexit) = open_read($hintfile);
	if ($rexit) {
		warn $rfh;
		exit $rexit;
	}
	my (@outgoing, $replaced);
	push @operation, $sbo if $replace_optional and not $clear;
	if ($optional and not $clear) {
		if (my @current = get_optional($sbo)) {
			for my $to_add (@operation) { push @current, $to_add; }
			@operation = @current;
		}
		@operation = uniq @operation;
		push @operation, $sbo;
	}
	wrapsay "Making changes for $sbo...";
	# read the hints file one line at a time; whether (and how) the
	# line is written back in to the file depends on the flags
	FIRST: for my $line (<$rfh>) {
		chomp(my $search = $line);
		my $target = (grep { /^$search$/ } @loclistings and $search =~ m/\s$sbo$/);
		unless ($clear) {
			# replace the first specification for $sbo, and then
			# skip further instances (flags: -O, -o)
			if ($target and not $replaced) {
				push @outgoing, join(" ", @operation);
				push @outgoing, "\n";
				$replaced = 1;
			} elsif ($target) {
				next FIRST;
			} else {
				push @outgoing, $line;
			}
		} elsif ($replace_optional) {
			# skip any line that specifies optional dependencies for
			# $sbo (flag: -cO)
			push @outgoing, $line unless $target;
		} else {
			# clear the requested scripts from any specification for
			# $sbo (flag: -co)
			if ($target) {
				for my $to_clear (@operation) { $line =~ s/(?<=^)$to_clear\s//g; }
				for my $to_clear (@operation) { $line =~ s/(?<=\s)$to_clear\s//g; }
			}
			push @outgoing, $line;
		}
	}
	# The simplest case of all is adding a completely new request.
	unless ($clear or $replaced) {
		my $new_line = join(" ",@operation);
		$new_line = "$new_line\n";
		push @outgoing, $new_line;
	}
	close $rfh;
	my ($fh, $exit) = open_fh($hintfile, '>');
	if ($exit) {
		warn $fh;
		exit $exit;
	}
	print {$fh} @outgoing;
	close $fh;
}
